/*
 * Copyright (c) 2017 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 * Copyright (c) 2007-2017 Xplenty, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.1'
    classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.6'
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'provided-base'

apply from: 'etc/version.gradle'
//apply from: 'etc/providedCompile.gradle'

archivesBaseName = 'cascading-bind'
group = 'cascading'
version = releaseVersion

ext.cascadingVersion = '3.2.+'

// set property to use locally installed cascading builds
// otherwise wip releases will be pulled from conjars
if( System.properties[ 'bind.release.local' ] )
  cascadingVersion = '3.2.0-wip-dev'

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'http://conjars.org/repo/' }
}

configurations {
  testArtifacts {
    extendsFrom testRuntime
  }
}

task sourcesJar( type: Jar, dependsOn: classes ) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar( type: Jar, dependsOn: javadoc ) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task testsJar( type: Jar, dependsOn: testClasses ) {
  from sourceSets.test.output
  classifier = 'tests'
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
  archives testsJar
  testArtifacts testsJar
}

dependencies {

  provided group: 'cascading', name: 'cascading-core', version: cascadingVersion, changing: true
  provided group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'

  // these are annotations, so are optional
  provided group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.1.1'

  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, changing: true
  testCompile group: 'cascading', name: 'cascading-core', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-platform', version: cascadingVersion, classifier: 'tests', changing: true

  testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.1.1'

  testCompile group: 'junit', name: 'junit', version: '4.11'

  testRuntime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
  testRuntime group: 'log4j', name: 'log4j', version: '1.2.16'
}

test {
  include '**/*Test.class'
}

// depends on etc/properties.gradle
uploadArchives {

  repositories.mavenDeployer {
    configuration = configurations.archives

    repository( url: repoUrl ) {
      authentication( userName: repoUserName, password: repoPassword )
    }

    pom.project {
      description 'An API for data management, analytics, and machine learning on parallel computing clusters.'
      inceptionYear '2011'
      url 'http://cascading.org/'
      scm {
        url 'https://github.com/Cascading/cascading.bind.git'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}

