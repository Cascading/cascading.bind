/*
 * Copyright (c) 2007-2012 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */





apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

archivesBaseName = 'cascading-bind'
group = 'cascading'
version = '1.0-wip-dev'

repositories {
  mavenLocal()
  mavenCentral()
  mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
}

configurations {
  testArtifacts {
    extendsFrom testRuntime
  }
}

task sourcesJar( type: Jar, dependsOn: classes ) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar( type: Jar, dependsOn: javadoc ) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task testsJar( type: Jar, dependsOn: testClasses ) {
  from sourceSets.test.output
  classifier = 'tests'
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
  archives testsJar
  testArtifacts testsJar
}

dependencies {

  def cascadingVersion = '2.1.0'

  compile group: 'cascading', name: 'cascading-core', version: cascadingVersion, changing: true

  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, changing: true
  testCompile group: 'cascading', name: 'cascading-core', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-platform', version: cascadingVersion, classifier: 'tests', changing: true

  testCompile group: 'junit', name: 'junit', version: '4.8.+'
}

test {
  include '**/*Test.class'
}

uploadArchives {

  def repoUrl = 'http://conjars.org/repo/'
  def repoUserName = System.properties[ 'publish.repo.userName' ]
  def repoPassword = System.properties[ 'publish.repo.password' ]

  if( System.properties[ 'publish.repo.url' ] )
    repoUrl = System.properties[ 'publish.repo.url' ]

  repositories.mavenDeployer {
    configuration = configurations.archives

    repository( url: repoUrl ) {
      authentication( userName: repoUserName, password: repoPassword )
    }

    pom.project {
      description 'An API for data management, analytics, and machine learning on parallel computing clusters.'
      inceptionYear '2011'
      url 'http://cascading.org/'
      scm {
        url 'https://github.com/Cascading/cascading.bind.git'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}

